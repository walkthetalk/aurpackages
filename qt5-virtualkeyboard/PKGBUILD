# $Id$
# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Antonio Rojas <arojas@archlinux.org>

pkgname=qt5-virtualkeyboard
_qtver=5.9.0
pkgver=${_qtver/-/}
pkgrel=1
arch=('i686' 'x86_64')
url='http://qt-project.org/'
license=('GPL3')
pkgdesc='Virtual keyboard framework'
depends=('qt5-declarative' 'qt5-svg' 'hunspell')
makedepends=()
groups=('qt' 'qt5')
_pkgfqn="${pkgname/5-/}-opensource-src-${_qtver}"
source=("http://download.qt.io/official_releases/qt/${pkgver%.*}/${_qtver}/submodules/${_pkgfqn}.tar.xz"
        'QTBUG-61308.patch')
sha256sums=('2974acf65e617d6b4f6288b70953673a9a68612c9caf7d642c370c038e3d7a76'
            'SKIP')
options=('!strip')

prepare() {
  mkdir -p build
  cd ${_pkgfqn}
  patch -p1 -i "${srcdir}/QTBUG-61308.patch"
}

build() {
  cd build

  qmake ../${_pkgfqn} CONFIG+="lang-all handwriting arrow-key-navigation debug"
  make -j8
}

package() {
  cd build
  make INSTALL_ROOT="$pkgdir" install

  # Drop QMAKE_PRL_BUILD_DIR because reference the build dir
  find "$pkgdir/usr/lib" -type f -name '*.prl' \
    -exec sed -i -e '/^QMAKE_PRL_BUILD_DIR/d' {} \;
}
